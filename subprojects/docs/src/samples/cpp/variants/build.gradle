apply plugin: 'cpp'

sources {
    exe {}
    lib {}
}

libraries {
    // Define some common settings for all shared library binaries
    all {
        binaries.withType(SharedLibraryBinary) {
            define "DLL_EXPORT"
        }
    }
    helloEnglish {
        source sources.lib
    }
    helloFrench {
        source sources.lib
        binaries.all {
            define "FRENCH"
        }
    }
}

executables {
    english {
        source sources.exe
        binaries.all {
            // Wire the 'helloEnglish' shared library binary into the 'english' executable binary
            lib libraries.helloEnglish.shared
        }
    }
    french {
        source sources.exe
        binaries.all {
            // Wire the 'helloFrench' static library binary into the 'french' executable binary
            lib libraries.helloFrench.static
        }
    }
}
